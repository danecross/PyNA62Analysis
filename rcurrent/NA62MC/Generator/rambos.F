      SUBROUTINE RAMBOS(N,ET,XM,P,WT)
C------------------------------------------------------
C
C                       RAMBO
C
C    RA(NDOM)  M(OMENTA)  B(EAUTIFULLY)  O(RGANIZED)
C
C    A DEMOCRATIC MULTI-PARTICLE PHASE SPACE GENERATOR
C    AUTHORS:  S.D. ELLIS,  R. KLEISS,  W.J. STIRLING
C    THIS IS VERSION 1.0 -  WRITTEN BY R. KLEISS
C
C    N  = NUMBER OF PARTICLES (>1, IN THIS VERSION <101)
C    ET = TOTAL CENTRE-OF-MASS ENERGY
C    XM = PARTICLE MASSES ( DIM=100 )
C    P  = PARTICLE MOMENTA ( DIM=(4,100) )
C    WT = WEIGHT OF THE EVENT
C
C------------------------------------------------------
      IMPLICIT NONE
      INTEGER N,ITMAX,IBEGIN,IWARN(5),I,ITER,K,NM
      REAL*8 ET,WT,A,ACC,ACCU,BQ,C,F,F0,G,G0,PO2LOG,RMAS,S,TWOPI,WT2,WT3
      REAL*8 WTM,X,X2,XMAX,XMT

      REAL*8 XM(100),P(4,100),Q(4,100),Z(100),R(4),
     &   B(3),P2(100),XM2(100),E(100),V(100)
      REAL*8 VEC(10)
      DATA ACC/1.E-14/,ITMAX/6/,IBEGIN/0/,IWARN/5*0/
C
C INITIALIZATION STEP: FACTORIALS FOR THE PHASE SPACE WEIGHT

      IF(IBEGIN.NE.0) GOTO 103
      IBEGIN=1
      TWOPI=8.*DATAN(DBLE(1.0))
      PO2LOG=DLOG(TWOPI/4.)
      Z(2)=PO2LOG
      DO 101 K=3,100
 101     Z(K)=Z(K-1)+PO2LOG-2.*DLOG(DBLE(K-2))
      DO 102 K=3,100
 102     Z(K)=(Z(K)-DLOG(DBLE(K-1)))
C
 103  CONTINUE
C
C CHECK WHETHER TOTAL ENERGY IS SUFFICIENT AND COUNT NONZERO MASSES
  104 XMT=0.
      NM=0
      DO 105 I=1,N
         IF(XM(I).NE.0.) NM=NM+1
  105 XMT=XMT+ABS(XM(I))
      IF(XMT.GT.ET) STOP
C
C THE PARAMETER VALUES ARE NOW ACCEPTED
C
C GENERATE N MASSLESS MOMENTA IN INFINITE PHASE SPACE
  201 DO 202 I=1,N
         CALL RANMAR(VEC,8)
         C=2.*VEC(1)-1.
         S=SQRT(1.-C*C)
         F=TWOPI*VEC(2)
         Q(4,I)=-DLOG(VEC(3)*VEC(4))
         Q(3,I)=Q(4,I)*C
         Q(2,I)=Q(4,I)*S*DCOS(F)
 202  Q(1,I)=Q(4,I)*S*DSIN(F)
C     
C CALCULATE THE PARAMETERS OF THE CONFORMAL TRANSFORMATION
      DO 203 I=1,4
 203  R(I)=0.
      DO 204 I=1,N
         DO 204 K=1,4
 204  R(K)=R(K)+Q(K,I)
      RMAS=DSQRT(R(4)**2-R(3)**2-R(2)**2-R(1)**2)
      DO 205 K=1,3
 205  B(K)=-R(K)/RMAS
      G=R(4)/RMAS
      A=1./(1.+G)
      X=ET/RMAS
C
C TRANSFORM THE Q'S CONFORMALLY INTO THE P'S
      DO 207 I=1,N
         BQ=B(1)*Q(1,I)+B(2)*Q(2,I)+B(3)*Q(3,I)
         DO 206 K=1,3
 206     P(K,I)=X*(Q(K,I)+B(K)*(Q(4,I)+A*BQ))
 207  P(4,I)=X*(G*Q(4,I)+BQ)
C
C CALCULATE WEIGHT AND POSSIBLE WARNINGS
      WT=PO2LOG
      IF(N.NE.2) WT=(2.*N-4.)*DLOG(ET)+Z(N)
      IF(WT.GE.-180.) GOTO 208
      IWARN(1)=IWARN(1)+1
  208 IF(WT.LE. 174.) GOTO 209
      IWARN(2)=IWARN(2)+1
C
C RETURN FOR WEIGHTED MASSLESS MOMENTA
  209 IF(NM.NE.0) GOTO 210
      WT=EXP(WT)
      RETURN
C
C MASSIVE PARTICLES: RESCALE THE MOMENTA BY A FACTOR X
  210 XMAX=SQRT(1.-(XMT/ET)**2)
      DO 301 I=1,N
         XM2(I)=XM(I)**2
  301 P2(I)=P(4,I)**2
      ITER=0
      X=XMAX
      ACCU=ET*ACC
  302 F0=-ET
      G0=0.
      X2=X*X
      DO 303 I=1,N
         E(I)=DSQRT(XM2(I)+X2*P2(I))
         F0=F0+E(I)
  303 G0=G0+P2(I)/E(I)
      IF(DABS(F0).LE.ACCU) GOTO 305
      ITER=ITER+1
      IF(ITER.LE.ITMAX) GOTO 304
      GOTO 305
  304 X=X-F0/(X*G0)
      GOTO 302
  305 DO 307 I=1,N
         V(I)=X*P(4,I)
         DO 306 K=1,3
 306     P(K,I)=X*P(K,I)
  307 P(4,I)=E(I)
C
C CALCULATE THE MASS-EFFECT WEIGHT FACTOR
      WT2=1.
      WT3=0.
      DO 308 I=1,N
         WT2=WT2*V(I)/E(I)
  308 WT3=WT3+V(I)**2/E(I)
      WTM=(2.*N-3.)*DLOG(X)+DLOG(WT2/WT3*ET)
C
C RETURN FOR  WEIGHTED MASSIVE MOMENTA
      WT=WT+WTM
      IF (WT.GE.-180.) GOTO 309
      IWARN(3)=IWARN(3)+1
  309 IF (WT.LE. 174.) GOTO 310
      IWARN(4)=IWARN(4)+1
  310 WT=EXP(WT)

      RETURN
      END
